/*
 * Демонстрационный пример сложного использования макро
 *
 * Ведение базы данных по сотрудникам
 * Имеется одно отношение "работник", в котором
 * хранятся номер, фамилия и инициалы, зарплата, годы рождения
 * и начала работы, номер начальника
 *
 * Для автоматического ведения номеров имеется отношение макс_номера,
 * в котором нас интересует поле "работник" - текущий номер нового
 * работника (он только возрастает).
 * Это отношение состоит из одного кортежа с внутренним номером "tid"
 * (адрес в файле), равным 0
 *
 * Для экранной работы определяем макро:
 * {CLR} - чистка экрана
 * {s0}, {s1}, {s2}, {s3} - установка различных режимов выделения текста
 * (предполагаем, что эти символы при выдаче на экран занимают 1 позицию
 * если это не так, нужно добавить туда пробел
 * {CS1} - установить область прокрутки экрана на строки 5-24, если это
 * вообще возможно
 *    К О М М Е Н Т А Р И И    К   П Р И М Е Р У
 *
 * 1. Лучшем стилем было бы вынести все описания макро в отдельный
 *    файл и подключать его по команде "\ i имя" (без пробела между \ и i).
 *    При этом поиск метки при переходах будет идти быстрее
 * 2. Обратите внимание, что в комментарии нельзя использовать команды
 *    монитора и макро - комментарий их не экранирует
 */
{define;{CLR};[H[J}
{define;{CS1};[5;24r}
{define;{s0};[p}
{define;{s1};[16p}
{define;{s2};[17p}
{define;{s3};[19p}
{remove;{continuetrap}}
{remove;нов}
/*
 * Макро нов служит для введения нового служащего
 */
{define; нов; \
/*\
{type @{CLR}} {type  @{s2}Вводим нового работника{s0} } {type }\
 {define;{фам};@{read @{s2}Фамилия И.О.?       {s1} .................... {s0}}}\
 {define;{нач};@{read @{s2}Начальник(Фамилия)? {s1} .................... {s0}}}\
{define;{годр};@{read @{s2}Год рождения?       {s1} .... {s0}}}\
{define;{годн};@{read @{s2}Год начала работы?  {s1} .... {s0}}}\
 {define;{зар};@{read @{s2}Зарплата?           {s1} .... {s0}}}\
 */{type }\
{type  @{s2}{фам} начальник= {нач} род. {годр} начал {годн} ставка {зар}{s0}}\
{define;{ask}; @{read Правильно (да/нет)?}}\
{ifsame;@{ask};да;{type Заносим!};/* {type Повторите}}\
range of n is макс_номера\
replace n(работник=n.работник+1) where (n.tid=0)\
range of r is работник\
append to работник (номер=n.работник,\
		    имя="{фам}",\
		    начальник=max(r.номер where r.имя="{нач} *"), \
		    родился = {годр},\
		    начал_работу = {годн},\
		    заработок={зар} \
) where (n.tid=0)\
{ifsame;@{ask};да;;*/}\
}
/*
 * Макро для запроса условий поиска/удаления
 */
{remove;усл}
{define;{where};1>0}
{define;выдусл;\
{type  @{s2} Текущее условие: {s0}} {type @{s1} {where}  {s0}}\
}
{define; усл; \
/*\
{type @{CLR}\\\
{s2}          Условия поиска/исключения. Введите    {s0}\\\
{s2}         Операцию {=/!=/>/</>=/<=} на месте "*",{s0}\\\
{s2}         Значение на месте "."                  {s0}\\\
{s2}  Или <ВК>, если по данному ключу условий нет   {s0} }\
{type }\
{define;{wh};}\
 {define;{фам};@{read @{s2}Фамилия И.О.?       {s1} ...................... {s0}}}\
	{ifgt;@{readcount};0;{define;{wh};@p.имя = "{фам}" and};}\
 {define;{нач};@{read @{s2}Начальник(Фамилия)? {s1} ...................... {s0}}}\
	{ifgt;@{readcount};0;{define;{wh};@{wh} p.начальник=r.номер and r.имя="{нач}" and};}\
{define;{годр};@{read @{s2}??Год рождения?       {s1} **.... {s0}}}\
	{ifgt;@{readcount};0;{define;{wh};@{wh} p.родился {годр} and};}\
{define;{годн};@{read @{s2}??Год начала работы?  {s1} **.... {s0}}}\
	{ifgt;@{readcount};0;{define;{wh};@{wh} p.начал_работу {годн} and};}\
 {define;{зар};@{read @{s2}??Зарплата?           {s1} **.... {s0}}}\
	{ifgt;@{readcount};0;{define;{wh};@{wh} p.заработок {зар} and};}\
{define;{wh};@{wh} 1>0}\
 */{type }\
{type  @{s2} Условие поиска: {s0}} {type @{s1} {wh}  {s0}}\
{define;{ask}; @{read Правильно (да/нет)?}}\
{ifsame;@{ask};да;{define;{where};@{wh}}{type Условие запомнено};{type Повторите}}\
}
\v\r
/*
 * Начало обработки
 *
 */
\branch start
/*
 * Продолжение - подождем, чтобы не очистить экран раньше времени
 */
\mark start1
{read Нажмите <ВК>, чтобы продолжить}
\v\r
\mark start
range of p is работник
range of r is работник
\g
{type @{CLR}{s2} Демонстрационная база КАДРЫ {s0}\
}
выдусл
{type @{CS1}\
\
\
\
\
	{s2}            Введите номер команды          {s0}\
	{s2}       0  - выйти                          {s0}\
	{s2}       1  - выдать информацию              {s0}\
	{s2}       2  - исключить информацию           {s0}\
	{s2}       3  - добавить информацию            {s0}\
	{s2}       4  - установить условия             {s0}\
	{s2}       5  - ввести список сотрудников      {s0}\
	{s2}       6  - сбросить условия               {s0}\
	{s2}       7  - вывести всю таблицу            {s0}\
}
\g
\r
{define;ответ;999}
{define;{ответ};@{read @{s2} Номер команды? {s1} .. {s0}}}
\g
\r
\branch ?{ответ}=0 exit
\branch ?{ответ}=1 retro
\branch ?{ответ}=2 del
\branch ?{ответ}=3 add
\branch ?{ответ}=4 set
\branch ?{ответ}=5 reads
\branch ?{ответ}=6 clr
\branch ?{ответ}=7 retall
\branch start
\mark set
усл
\g
\branch start1
\mark retro
retrieve (p.all) where ({where})
\g
\branch start1
\mark del
{type @{s1} Сначала посмотрите внимательно, то ли вы исключаете {s0}}
retrieve (p.all) where ({where})
\g
\branch ?{ifsame;@{read Исключать (да/нет)?};да;0;1}=1 start
delete p where ({where})
\g
\branch start1
\mark add
нов
\g
{ifeq;@{tuplecount};1;;{type @{s3}Недопустимые данные, запрос не выполнен{s0}}}
retrieve (r.all) where (r.имя = "{фам}")
\g
\branch start1
\mark reads
/*
 * Ввод служащих в табличной форме
 */
\r
{type @{CLR}{s2}Вводим нового работника{s0}(поля ввода добивать пробелами)}
	       {type @{s2}                     {s2}                    {s2}год {s2}нач {s2}зар  {s0}}
	       {type @{s2}  Фамилия И.О.       {s2}  Начальник(фамилия){s2}рожд{s2}раб {s2}плата{s0}}
\mark read1
{readdefine;{str};@{s2} ................... {s2}................... {s2}... {s2}... {s2}.. {s0}{s1}}
\v\r
\b ?({readcount}<=0) start
{define;{фам};@{substr;1;20;@{str} }}
{define;{нач};@{substr;22;41;@{str} }}
{define;{годр};@{substr;43;46;@{str} }}
{define;{годн};@{substr;48;51;@{str} }}
{define;{зар};@{substr;53;55;@{str} }}
\v\r
{define;{ask}; @{read @{s2} {фам} {нач} {годр} {годн} {зар} {s0} ([да]/нет)?}}
{ifsame;@{ask};нет;/*;}
range of n is макс_номера
replace n(работник=n.работник+1) where (n.tid=0)
range of r is работник
append to работник (номер=n.работник,
		    имя="{фам}",
		    начальник=max(r.номер where r.имя="{нач} *"),
		    родился = {годр},
		    начал_работу = {годн},
		    заработок={зар}
) where (n.tid=0)
{ifsame;@{ask};нет;*/;}
\g
{ifeq;@{tuplecount};1;;{type @{s3}Недопустимые данные, запрос не выполнен{s0}}}
\v\r
\b read1
\mark retall
retrieve (p.all)
\g
\branch start1
\mark clr
{define;{where};1>0}
\v\r
\b start
\mark exit
{type Конец работы}\v\r
