head     1.1;
access   ;
symbols  ;
locks    ;
comment  @/ @;


1.1
date     88.06.12.15.07.28;  author dvolodin;  state Exp;
branches ;
next     ;


desc
@
Оверлейная версия для setjmp.
@



1.1
log
@Initial revision
@
text
@/
/ $Log:	setjmp.s,v $
/ Revision 1.1  86/04/21  20:06:35  root
/ Initial revision
/ 
/

/ C library -- setjmp, longjmp
/ Overlay version

/       longjmp(a,v)
/ will generate a "return(v)" from
/ the last call to
/       setjmp(a)
/ by restoring sp, r5, pc from `a'
/ and doing a return.
/

.globl  _setjmp
.globl  _longjmp
.globl  csv, cret
.globl  __ovno

_setjmp:
	mov     2(sp),r0
	mov     r5,(r0)+
	mov     sp,(r0)+
	mov     (sp),(r0)+
	mov     __ovno,(r0)
	clr     r0
	rts     pc

_longjmp:
	jsr     r5,csv
	mov     4(r5),r1
	mov	6(r5),r0
	bne	1f
	mov	$1,r0
1:
	cmp	(r5),(r1)
	beq	1f
	mov	(r5),r5
	bne	1b
/ panic -- r2-r4 lost
	mov	(r1)+,r5
	mov	(r1)+,sp
	mov     (r1)+,(sp)
emt= 0104000            / overlays switched by emulator trap. ovno in r0.
	mov     (r1),r0
	emt
	mov     r0,__ovno
	rts	pc
1:
	mov	4(r1),2(r5)
	mov     6(r1),-2(r5)
	jmp	cret

@
